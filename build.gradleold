plugins {
    id 'java'
    id 'io.papermc.paperweight.userdev' version '1.5.5'
    id 'org.jetbrains.kotlin.jvm' version '1.9.10'
    id 'com.github.johnrengelman.shadow' version '8.1.1'
}

group = 'me.ryanlauderbach'
version = '1.0.0'

repositories {
    mavenCentral()
    maven {
        name = 'sonatype'
        url = 'https://oss.sonatype.org/content/groups/public/'
    }
}

dependencies {
    paperweightDevelopmentBundle 'io.papermc.paper:dev-bundle:1.20.2-R0.1-SNAPSHOT'
    implementation 'org.jetbrains.kotlin:kotlin-stdlib-jdk8:1.9.10'
}

def targetJavaVersion = 17
java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
}

tasks.withType(JavaCompile).configureEach {
    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        options.release = targetJavaVersion
    }
}

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('plugin.yml') {
        expand props
    }
}

assemble {
    dependsOn(reobfJar)
}

compileKotlin {
    kotlinOptions.jvmTarget = "17"
}
compileTestKotlin {
    kotlinOptions.jvmTarget = "17"
}

tasks.register('copyJar', Copy) {
    dependsOn(build)
    from file("build/libs/survivaltools-${version}.jar")
    into file("~/Minecraft/Testing/plugins")
}
